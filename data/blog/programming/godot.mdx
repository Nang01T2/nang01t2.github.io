---
title: Godot 1
date: '2021-05-02'
tags: ['programming', 'godot', 'videogames']
---

![Godot Logo](/static/images/godot/GodotLogo.png)

The [Godot](https://godotengine.org/download/windows) engine is a free, open source, videogame engine with many [assets](https://godotengine.org/asset-library/asset). To know more you can visit their [blog](https://godotengine.org/news), or use one of their [community channels](https://godotengine.org/community). To actually learn Godot you can use their [docs](https://docs.godotengine.org/en/stable/).

- 3D engine with MSAA, BSDF, scattering, anisotropy etc.
- 2D engine with title maps, grid shape, layers, lights etc.
- Official languages GDScript, C#, C++, VisualScript
- Export to iOS, Android, Windows, maxOS, Linux, UWP, BSD, Haiku.
- Files with Git, Subversion, Mercurial, PlasticSCM, Perforce.
- Plugins for Python and Nim compatibility.
- Blender models exported with Collada

## Starting Godot

When you open Godot you encounter the **Project Administrator**, use it to edit, play, search, create, import, rename or delete your projects. 

![Godot First Open](/static/images/godot/project_manager_first_open.png)

The templates section has online starters.  

![Godot Templates](/static/images/godot/editor_ui_intro_project_manager_03.png)

Every project needs a unique name, folder and selected backend (GLES2 or GLES3).

![Godot Project Manager](/static/images/godot/editor_ui_intro_project_manager_04.png)

To import a project use the search option and open the folder that contains a `project.godot` file.

![Godot Project Import](/static/images/godot/editor_ui_intro_project_manager_05.png)

When you open a project you encounter the **Project editor** that is divided in:

![Godot Editor Overview](/static/images/godot/editor_ui_intro_editor_interface_overview.png)

- Main Menus: Scene, Project, Debug, Editor, Help.
- Workspaces: 2D, 3D, Script, Assets.
- Playtest: Play project, pause, stop, play scene, play custom.
- Toolbar: Reacts to the selected workspace.
- Viewport: How the game currently looks.
- Bottom Panel: Output, Debugger, Animation, Audio.
- Docks:
	* Scene: Shows every node of the current scene.
	* Import: Properties when importing an asset to the selected node.
	* FileSystem: Organize files and assets of the project.
	* Inspector: Classes, Sections and Properties of the selected node.
	* Node: Implementation, functions and signals of the selected node.

With the **Scene Menu** you create, open, save, reload and close scenes (with `tscn` extension). With the **Project Menu** you can modify the settings, select a version control or export the project. With the **Editor Menu** you can manage export templates, and control multiple aspects of the editor itself across projects.

***

## Scenes and Nodes

A **Node** is the basic unit of Godot, every node has universal attributes:

1. A unique name.
2. Can have editable properties.
3. Can receive a callback each frame.
4. Can be extended with functions.
5. Can be the child of other node.

A **Scene** is the hierarchical organization of nodes to create a tree of nodes:

1. A scene has a root node.
2. Is is independent of other scenes with a unique file.
3. Can be instanced or used inside another scene as a node.

In the project settings `Aplication-Run-MainScene` we can select the MainScene. When we run a Godot project what we are actually doing is instancing the root node of the MainScene.

![Godot Main Scene](/static/images/godot/main_scene.png)

### Instancing example

In [this example](https://docs.godotengine.org/en/stable/_downloads/0f77439a03c9c1eb57dab630d028b82d/instancing.zip) we have a Main scene and a Ball scene. Ball is composed of a `RigidBody2D` *ball* node, while Main is composed of multiple `StaticBody2D` *obstacle* nodes. We can add Ball to Main like every other node using the link button.

![Godot Link Button](/static/images/godot/instancing_linkbutton.png)
![Godot Multiball](/static/images/godot/instancing_multiball.png.png)

If we modify Ball every instance of Ball inside Main will be updated with the new information, if we want to change a particular instance we indicate it with the **Make Unique** option, then a gray icon indicates that *this* parameter is different from the original Ball scene.

![Godot Return Button](/static/images/godot/instancing_property.png)

This design system of nodes and scenes focuses on the visual elements of the screen. It is therefore recommended to create a basic diagram of every visual element that will be expected and how they will relate to each other.

![Node Relationships](/static/images/godot/NodeRelations.jpg)

The lowest elements of the diagram are the ones expected to be described using nodes, meaning that most scenes will be composed of other scenes.

![Scene Relationships](/static/images/godot/SceneRelations.png)

***

## My First Script

The official scripting language is GDScript (loosely based on Python), let's start by creating a button that when pressed displays *Hello World!*. We will add three nodes to the MainScene:

1. Panel: Rectangular opaque background used to contain other Control nodes. It inherits from `Control < CanvasItem < Node < Object`.
	1. Button: Default button, its methods like `_pressed()` come from `BaseButton`. It inherits from `BaseButton < Control < CanvasItem < Node < Object`.
	2. Label: Rectangular plain text box. We will change its `text` property. It inherits from `Control < CanvasItem < Node < Object`.

![Script Added](/static/images/godot/script_added.png)

With all three nodes created we can add a script to the Panel node. All nodes than inherit from `Node` trigger a `_ready()` callback when they finish loading into the scene tree. Note that `_ready()` is not the node constructor but `_init()` instead, also note that the new script inherits everything from its parent node.

![Script Template](/static/images/godot/script_template.png)

Nodes naturally emit **Signals** when a specific action happens to them, the signals can then be **connected** to functions inside scripts that execute an action afterwards. We can implement our own signals if the default nodes do not emit a signal when we want.

![Signals](/static/images/godot/signals.png)

Luckily the Button node emits the `_pressed()` signal. To connect the signal to the script we will use the `Node.get_node()` function, this will fetch the `Object Path` of the Button node relative to the script, then we will use the `Object.connect()` function to listen for `_pressed()` and finally execute the function `_on_Button_pressed()` each time the signal is emitted.

```gdscript
extend Panel

func _ready():
    get_node('Button').connect('pressed', self, '_on_Button_pressed')

func _on_Button_pressed():
    get_node('Label').text = 'Hello'
```

There are two ways of saving an `Object Path` into a variable for multiple uses across the script:

```gdscript
# First way
var my_label
func _ready():
	my_label = get_node("MyLabel")

# Second way
onready var my_label = get_node("MyLabel")
```

### Processing

Godot includes two functions that are executed continuously. 

The first one is `Node._process()` and when implemented on any script it activates Godot's idle processing calling it every **Idle Frame**, in other words the frequency of the calls will vary depending on the FPS of the application. If the implementation needs to simulate a constant action the **Delta** parameter counts the time that each frame took so that you can scale the action accordingly. Note that once activated you can stop Godot's idle processing with `Node.set_process()`.

The second one is `Node._physics_process()`, this one is called each time before a **Physics Step** is calculated, for example to check if a key is still being pressed. By default 60 physics steps are calculated per second. You can change this setting on `Project Settings - Physics - Common - Physics FPS`. Note that even in an optimized game `_process()` will be called *after* the physics step.

### Groups

We can group nodes with `Node.add_to_group()`, groups allows us to execute a function implemented by its members with `SceneTree.call_group()`. 

For example if every *enemy* member of a *enemies* group listens for a *discovered* signal emitted by the *player* then the first *enemy* intercepts the signal it can send an order to the *enemies* group members to execute their `player_was_discovered` function.

```gdscript
func _ready():
    add_to_group('enemies')

func _on_discovered():
    get_tree().call_group('enemies', 'player_was_discovered')
```

You can get a list of every member in a group with the `SceneTree.get_nodes_in_group()` function. Note that the `SceneTree` object includes other useful methods for manipulating nodes, changing the scene and pausing or exiting the game. 

### Notifications

On a low level, methods like `_ready()` are actually *overridable* virtual methods that get called through a notification system, you may need to access the notification system directly.

```gdscript
func _notification(what):
    match what:
        NOTIFICATION_READY:
            print("Same as implementing _ready().")
```

Overridable `Node` methods:

1. `_enter_tree()`: Called when only the node finishes loading becomes active. 
2. `_ready()`: Called after `_enter_Tree()`, when every child node has been loaded and become active.
3. `_exit_tree()`: Called when the node and every child node has unloaded and become inactive.
4. `_process(delta)`: Called every idle frame as soon as it can.
5. `_physics_process(delta)`: Called every before every physics step.

### Nodes

To create new nodes we use `Node.new()`. To add the node as a child of the Script's node we use `Node.add_child()`. To delete a node and immediately we use `Note.free()`. Note that freeing a node while it's emitting a signal or executing a function will terminate the app. To wait for the signal or function to finish before freeing the node we use `Node.queue_free()`.

```gdscript
var s
func _ready():
    s = Sprite.new()
    add_child(s)

func _someaction():
    s.queue_free()
```

### Scenes

To link a scene we first need to load its file into memory as a `PackedScene` object, we can do this while parsing the script with `preload()` or after the script's node has loaded into the scene with `load()`. 

Then we convert the `PackedScene` object into a tree of nodes using the `PackedScene.instance()` method. This is a two step process because when we have the scene already loaded into memory we can instance it multiple times with minimal performance cost.

```gdscript
var scene = load("res://myscene.tscn") 
var node = scene.instance()
func _ready():
	add_child(node)
```

We can load a script file inside another script or we can register the script as a new `Class` and then extend it. To create a new class we use the keyword `class_name` and an optional icon. Registering a class means makes it global and can be accessed anywhere, however it cannot be unloaded from memory and you may run into a cyclic reference error: You can't reference a registered class *foo* in the fields of another registered class *bar* if *bar* has referenced *foo* or another script that depends on *foo*.

```gdscript
extends Node
class_name MyClassName, "res://path/to/icon16x16.png"
func _ready():
    var this = MyClassName
    var cppNode = MyCppNode.new()
	cppNode.queue_free()
```

### Signals

The *observer* pattern focuses on the emitters and their signal rather than on the listener continuously checking on an action, in this way the listener can wait idle and then act upon hearing the signal. 

The relationship between emitter and listener is *decoupled* meaning that in a way the emitter is sending its signal to no one in particular and the same with the listener.

```gdscript
# Blinking Sprite
extends Node
onready var OneSecondTimer = $Timer
onready var MainSpriteImage = $Sprite
func _ready():
    OneSecondTimer.connect("timeout", self, "_on_Timer_timeout")
func _on_Timer_timeout() -> void:
    MainSpriteImage.visible = !MainSpriteImage.visible
```

Note the use of the dollar sign as shorthand code for `Node.get_node()`. 

To custom signals we declare them with the keyword `signal`.

```gdscript
# Now this node emits my_signal upon loading into the scene.
extends Node
signal my_signal(value, other_value)
func _ready():
    emit_signal("my_signal", true, 42)
```

***

## My First Game

We will create the [Dodge the Creeps](https://docs.godotengine.org/en/stable/_downloads/e79a087a28c8eb4d140359198a122c0f/dodge_assets.zip) game. 

* Create the scenes **Main**, **Player**, **HUD** and **Mob**. 
* Change `width` to 480 and `height` to 720 inside `Proyect - Settings - Display - Window`.
* Set the `Mode` to 2D and the `Apect` to keep. This will allow rescaling of the window.

In the **Player** scene we:

1. Create an Area2D and rename it Player. This area will detect collisions via overlaps.
2. Select the option in the toolbar to make the object's children non selectable.
3. Add an AnimatedSprite to the Area2D. This will render the appearance of the player.
4. In the AnimatedSprite Frames add a SpriteFrames resource.
5. In the SpriteFrames panel add 2 animations, one called `up` and another called `walk`.
6. Add the corresponding pair of images to each animation.
7. Reduce the `AnimatedSprite - Node2D - Scale` to half (0.5, 0.5).
8. Add a CollisionShape2D to the Area2D. This will be the hitbox shape for the collisions.
9. In the CollisionShape2D Shape add a CapsuleShape2D.
10. Adjust the size of the Capsule in the viewport area.
11. Create a script for the Player node that extends Area2D.

```gdscript
extends Area2D
signal hit
var screen_size
# Touchscreen target
var target = Vector2()
# Speed in pixels/sec
export var speed = 400  

func _ready():
	# Get the viewport screen size
	screen_size = get_viewport_rect().size
	# Hide the player until we start a new round
	hide()
	# Listen itself for the Area2D body_entered signal
	self.connect("body_entered", self, "_on_Player_body_entered")

# If touchscreen is detected update target's location
func _input(event):
	if event is InputEventScreenTouch and event.pressed:
		target = event.position

func _process(delta):
	# Stop all movement, movement vector 0
	var velocity = Vector2(0,0) 
	
	# If the OS isn't windows then enable touchscreen
	if OS.get_name() != "Windows":
		# Use the updated target to modify movement vector
		if position.distance_to(target) > 10:
			velocity = target - self.position
	
	# If touchscreen isn't active use keyboard instead
	# Modify movement vector based on key pressed
	if Input.is_action_pressed("ui_right"):
		velocity.x += 1
	if Input.is_action_pressed("ui_left"):
		velocity.x -= 1
	if Input.is_action_pressed("ui_down"):
		velocity.y += 1
	if Input.is_action_pressed("ui_up"):
		velocity.y -= 1
	
	# If vector is not 0 then keys are being pressed
	if velocity.length() > 0:
		# Correct for diagonal movement
		velocity = velocity.normalized() * speed
		# Play the animation, a key is being pressed
		$AnimatedSprite.play()
	else:
		# Keys are not being pressed, stop the animation
		$AnimatedSprite.stop()
		
	# Change Player's position based on the current vector state.
	self.position += velocity * delta
	# Limit Player's position to the screen size.
	self.position.x = clamp(self.position.x, 0, screen_size.x)
	self.position.y = clamp(self.position.y, 0, screen_size.y)
	
	if velocity.x != 0:
		# For right or diagonal use Walk and disable V-Flip
		$AnimatedSprite.animation = "walk"
		$AnimatedSprite.flip_v = false
		# If left use Walk but activate H-Flip.
		$AnimatedSprite.flip_h = velocity.x < 0
	elif velocity.y != 0:
		# For up use Up. For down use Up but activate V-Flip.
		$AnimatedSprite.animation = "up"
		$AnimatedSprite.flip_v = velocity.y > 0

# Function executed when itself emits body_entered
func _on_Player_body_entered(body):
	# Turn off collisions, stop additional emissions of the signal
	# Deferred makes a queue because they may be multiple collisions
	$CollisionShape2D.set_deferred("disabled", true)
	# Hide the player, it has died
	hide()
	# Emit the hit by enemy signal to other control nodes
	emit_signal("hit")

# Function executed when we start a new round
func start(pos):
	# Turn collisions back on
	$CollisionShape2D.disabled = false
	# New round, go to starting position and show the player
	self.position = pos
	target = pos
	show()
```

In the **Mob** scene we:

1. Create a RigidBody2D and name it Mob, modify its gravity to 0.
2. Select `PhysicsBody2D - Mask` and disable the first option
	1. We do this to disable collisions between enemies.
2. Select the option in the toolbar to make the object's children non selectable.
3. Add an AnimatedSprite. In the SpriteFrames panel add `fly`, `swim` and `walk`.
4. Change the speed to 3 FPS and preview with the inspector option `playing`.
5. Reduce the `AnimatedSprite - Node2D - Scale` to (0.75, 0.75).
6. Add a CollisionShape2D and another CapsuleShape2D to it. 
7. Rotate and adjust the CapsuleShape2D in the viewport.
8. In the CollisionShape2D Shape add a CapsuleShape2D.
9. Create a VisibilityNotifier2D node that emits the `screen_exited` signal.
10. Create a script for the Mob node that extends RigidBody2D.

```gdscript
extends RigidBody2D
# Minimum and maximum randomized starting speed
export var min_speed = 150 
export var max_speed = 250

func _ready():
	# Add to mobs group 
	self.add_to_group("mobs")
	# Assign a random animation and play it
	var mob_types = $AnimatedSprite.frames.get_animation_names()
	$AnimatedSprite.animation = mob_types[randi() % mob_types.size()]
	$AnimatedSprite.play()
	$VisibilityNotifier2D.connect("screen_exited", self, "_on_Visibility_screen_exited")
	
# When this particular mob exits the screen delete it
func _on_Visibility_screen_exited():
	queue_free()
```

The function `randi()%n` selects a random integer between 0 and (n-1). Note however that `randi()` always has the same seed, in order to change the seed we will need to add the function `randomize()` later into the **Main** scene.

In the **Main** scene we:

1. Create a Node and call it Main.
2. Add a ColorRect and a Position2D call it StartPosition (240, 450).
	1. You can use a texture instead of a color with TextureRect.
3. Add 2 AudioStreamPlayer and call them Music and DeathSound.
4. Timers are MobTimer (spawn), ScoreTimer (score per sec) and StartTimer (delay). 
5. MobTimer (0.5), ScoreTimer (1 One Shot), StartTimer (2)
6. Instance Player and Mob scenes using the link button.
7. Add a Path2D (MobPath) and select the 4 corners of the screen.
8. Close the path clockwise so that the mob spawning happens to its right.
9. Add a PathFallow2D (MobSpawnLocation) as a child of MobPath.
	1. This location will move along the path spawning new mobs.
10. Create a script for Main node that extends Node.

```gdscript
extends Node
# Import the Mob scene into memory
export (PackedScene) var Mob
var score

func _ready():
	# Start a round when we click the HUD button
	$HUD.connect("start_game", self, "new_game")
	# When the Player is hit stop the round
	$Player.connect("hit", self, "game_over")
	# Functions to execute when the timers run out
	$StartTimer.connect("timeout", self, "_on_StartTimer_timeout")
	$ScoreTimer.connect("timeout", self, "_on_ScoreTimer_timeout")
	$MobTimer.connect("timeout", self, "_on_MobTimer_timeout")
	# Randomize the randi seed across all nodes
	randomize() 

# When the player is hit stop all timers and delete all mobs
# Stop music, play death sound and show the game over text
func game_over():
	$ScoreTimer.stop()
	$MobTimer.stop()
	get_tree().call_group("mobs", "queue_free")
	$DeathSound.play()
	$Music.stop()
	$HUD.show_game_over()

# After the delay start spawing mobs and start the score
func _on_StartTimer_timeout():
	$MobTimer.start()
	$ScoreTimer.start()

# Add to the score each time the score timer runs out
func _on_ScoreTimer_timeout():
	score += 1
	$HUD.update_score(score)

# Spawn a new mob using the timer on a random location
func _on_MobTimer_timeout():
	# Set a random location along the Path 2D
	$MobPath/MobSpawnLocation.offset = randi()
	# Spawn a mob and put its direction inward
	var mob = Mob.instance()
	add_child(mob)
	mob.position = $MobPath/MobSpawnLocation.position
	var direction = $MobPath/MobSpawnLocation.rotation + PI / 2
	# Add a little randomness to the ortogonal direction
	direction += rand_range(-PI / 4, PI / 4)
	mob.rotation = direction
	# Add a velocity vector with a random speed from the allowed interval
	mob.linear_velocity = Vector2(rand_range(mob.min_speed, mob.max_speed), 0)
	# Start a constant movement using the direction and the velocity vector
	mob.linear_velocity = mob.linear_velocity.rotated(direction)

# When we start a new game restart the score and the player position.
# Restart the delay, show the start game text and start the music
func new_game():
	score = 0
	$Player.start($StartPosition.position)
	$StartTimer.start()
	$HUD.update_score(score)
	$HUD.show_message("Get Ready")
	$Music.play()
```

In the **HUD** scene we:

1. Create a CanvasLayer and call it HUD. Add label (ScoreLabel).
3. Add label (MessageLabel), button (StartButton) and timer (MessageTimer).
4. Use the toolbar Layout button or adjust the anchors and margins manually.
	1. ScoreLabel, Top Wide, align Center, text 0, font Xolonium, size 64.
	2. MessageLabel, HCenter Wide, align Center, autowrap on.
	3. StartButton, Center Bottom, Margin Top -200, Bottom -100.
	4. MessageTimer, Wait Time 2 (One Shot)
5. Add a Space key shortcut in the StartButton properties. 
6. Create a script for HUD node that extends CanvasLayer.

```gdscript
extends CanvasLayer
# Emit this signal when the start button is used
signal start_game

func _ready():
	$StartButton.connect("pressed", self, "_on_StartButton_pressed")
	$MessageTimer.connect("timeout", self, "_on_MessageTimer_timeout")

# Show a message as long as the message timer is running
func show_message(text):
	$Message.text = text
	$Message.show()
	$MessageTimer.start()

# If the game ends show a game over text and 
func show_game_over():
	show_message("Game Over")
	# After the game over text display the main text
	yield($MessageTimer, "timeout")
	$Message.text = "Dodge the\nCreeps!"
	$Message.show()
	# After a brief delay show the start button
	yield(get_tree().create_timer(1), "timeout")
	$StartButton.show()

# Function used to update the score elsewhere
func update_score(score):
	$ScoreLabel.text = str(score)

# Hide the button and start the game after being pressed
func _on_StartButton_pressed():
	$StartButton.hide()
	emit_signal("start_game")

# Hide the message text after its timer runs out
func _on_MessageTimer_timeout():
	$Message.hide()
```

Note that control nodes have position and size but also have anchors and margins. Anchors define the origin, the reference point used to define the edges of the node. Margins update automatically as you move or resize the node, they represent the distance from the edges to their anchor.

### Export The Game

To enable touch emulation with the mouse we activate `Project Settings - InputDevices - Pointing`. 

To select the export targets we use `Editor - Manage Templates`. To start the exporting process we use `Project - Export`, we create a new export preset and select the desired platform. For Windows, Linux and MacOS exporting creates the executables directly. For IOS you need a Mac, Xcode and an App Store Team ID. HTML creates all the necessary files but needs a running server, create one quickly with `python -m http.server 9000`. For Android we need to have AndroidSDK and JavaJDK installed, then when we install AndroidStudio and AndroidSDKPlatformTools we get access to the `adb` command that Godot needs:

```gdscript
keytool -keyalg RSA -genkeypair -alias androiddebugkey -keypass android -keystore debug.keystore -storepass android -dname "CN=Android Debug,O=Android,C=US" -validity 9999
adb install dodge.apk
```